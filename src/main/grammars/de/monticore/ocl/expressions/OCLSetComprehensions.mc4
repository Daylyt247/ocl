/* (c) https://github.com/MontiCore/monticore */
package de.monticore.ocl.expressions;

/* Alpha-version: This is intended to become a MontiCore stable grammar. */

import de.monticore.expressions.*;

/**
 * This grammar defines a expressions typical to UMLs OCL
 */
component grammar OCLSetComprehensions
          extends OCLExpressionsBasis
{
  /**
   * ASTOCLComprehensionExpression defines comprehension kinds.
   *   e.g.:
   *       - {x * x | x in y} (OCLComprehensionExpressionStyle)
   *       - {1..3, x..10, y} (OCLComprehensionEnumerationStyle)
   */
  interface OCLComprehensionExpression;

  /**
   * ASTOCLComprehension represents comprehensions.
   *   @attribute SimpleReferenceType
   *              Optional type of comprehension, e.g. Set, List or Collection.
   *   @attribute expression
   *              Expression of comprehension.
   *   @attribute qualification
   *              Optional Qualification.
   */
  OCLComprehension implements OCLPrimary  <40> =
    MCType?
    "{" expression:OCLComprehensionExpression? "}"
    ("." qualification:OCLPrimary)?
    ;

  /**
   * ASTOCLComprehensionExpressionStyle defines a comprehension with given
   *   characteristic.
   *   @attribute expression
   *              An expression (e.g. "x*x" or "x in Y").
   *   @attribute comprehensionItems
   *              Characterization of comprehension as a list of
   *              comprehension-items. This can be generators, vardefinitions
   *              or filters.
   *   Note that we assume at least one generator (e.g. x in Y) in this AST.
   */
  OCLComprehensionExpressionStyle implements OCLComprehensionExpression <20> =
    Expression "|"
    (OCLComprehensionItem || ",")+
    ;

  OCLComprehensionItem =
    generator:InExpression |
    declaration:OCLVariableDeclaration |
    filter:Expression
    ;

  /**
   * ASTOCLComprehensionEnumerationStyle is used for an enumeration of
   *   comprehension elements. Note that collection items are optional.
   *   @attribute collectionItems
   *              Enumerated elements as a list separated by , (comma).
   *              (e.g.: "1..3, x, y..z")
   */
  OCLComprehensionEnumerationStyle implements OCLComprehensionExpression <10> =
    (OCLCollectionItem || ",")*
    ;

  interface OCLCollectionItem;

  // list of allowed values
  OCLSetValueList implements OCLCollectionItem =
    (Expression || ",")+
    ;

  // range of allowed values
  OCLSetValueRange implements OCLCollectionItem =
    lowerBound:Expression ".." upperBound:Expression
    ;
}
