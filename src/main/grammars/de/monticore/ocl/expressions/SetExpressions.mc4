/* (c) https://github.com/MontiCore/monticore */
package de.monticore.ocl.expressions;

/* Alpha-version: This is intended to become a MontiCore stable grammar. */

import de.monticore.expressions.*;
import de.monticore.types.*;
import de.monticore.statements.*;


/**
 * This grammar defines a expressions typical to UMLs OCL
 */
component grammar SetExpressions
          extends ExpressionsBasis,
                  MCBasicTypes,
                  MCVarDeclarationStatements
{
  /*=================================================================*/

  SetInExpression implements Expression <190> =
    elem:Expression
    operator:"in"
    set:Expression;

  /*=================================================================*/

  UnionExpression implements Expression <180> =
    left:Expression
    operator:"union"
    right:Expression;

  IntersectionExpression implements Expression <180> =
    left:Expression
    operator:"intersect"
    right:Expression;

  // We do not introduce a special operator for the
  // difference, because the binary "-" operator can here be used as well

  /*=================================================================*/

  // sets of sets united (i.e. flattened) and intersected
  SetUnionExpression implements Expression <170> =
    "union" set:Expression;

  SetIntersectionExpression implements Expression <170> =
    "intersect" set:Expression;

  /*=================================================================*/

  // Logical expressions extended to sets of arguments
  SetAndExpression implements Expression <130> =
    "setand" set:Expression;

  SetOrExpression implements Expression <130> =
    "setor" set:Expression;

  /*=================================================================*/

  /**
   * ASTSetComprehensionInner defines comprehension kinds.
   *   e.g.:
   *       - {x * x | x in y} (SetComprehensionInnerStyle)
   *       - {1..3, x..10, y} (SetComprehensionInnerStyle)
   */
  interface SetComprehensionInner;

  /**
   * ASTSetComprehension represents comprehensions.
   *   @attribute MCType
   *              Optional type of comprehension, e.g. Set, List or Collection.
   *   @attribute expression
   *              Expression of comprehension.
   *   @attribute qualification
   *              Optional Qualification.
   */
  SetComprehension implements Expression  <40> =
    MCType?
    "{" expression:SetComprehensionInner? "}"
    ("." qualification:Expression)?
    ;

  /**
   * ASTSetComprehensionInnerStyle defines a comprehension with given
   *   characteristic.
   *   @attribute expression
   *              An expression (e.g. "x*x" or "x in Y").
   *   @attribute comprehensionItems
   *              Characterization of comprehension as a list of
   *              comprehension-items. This can be generators, vardefinitions
   *              or filters.
   *   Note that we assume at least one generator (e.g. x in Y) in this AST.
   */
  SetComprehensionExpressionStyle implements SetComprehensionExpression <20> =
    Expression "|"
    (SetComprehensionItem || ",")+
    ;

  SetComprehensionItem =
    generator:Expression |
    declaration:LocalVariableDeclaration |
    filter:Expression
    ;

  /**
   * ASTSetComprehensionInnerStyle is used for an enumeration of
   *   comprehension elements. Note that collection items are optional.
   *   @attribute collectionItems
   *              Enumerated elements as a list separated by , (comma).
   *              (e.g.: "1..3, x, y..z")
   */
  SetComprehensionInnerStyle implements SetComprehensionExpression <10> =
    (SetCollectionItem || ",")*
    ;

  interface SetCollectionItem;

  // list of allowed values
  SetValueItem implements SetCollectionItem =
    (Expression || ",")+
    ;

  // range of allowed values
  SetValueRange implements SetCollectionItem =
    lowerBound:Expression ".." upperBound:Expression
    ;
}
